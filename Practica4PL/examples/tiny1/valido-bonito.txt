type pointer tNodo tArbol;
type record {
    string nombre;
    tArbol izq;
    tArbol der
} tNodo;
type record {
    array [50] of string nombres;
    int cont
} tListaNombres;
var tListaNombres nombres;
var tArbol arbol;

proc lee_nombres (tListaNombres & nombres) {
    var int i
    &&
    write "Introduce el número de nombres a ordenar (max 50): ";
    nl;
    read nombres.cont;
    
    while nombres.cont < 0 or nombres.cont > 50 do
        write "Introduce el número de nombres a ordenar (max 50): ";
        nl;
        read nombres.cont
    endwhile;
    i = 0;
    write "Introduce un nombre en cada línea: ";
    nl;
    
    while i < nombres.cont do
        read nombres.nombres[i];
        i = i + 1
    endwhile
};

proc construye_arbol () {
    var int i;
    
    proc inserta_nombre (tArbol & arbol) {
        if arbol == null then
            new arbol;
            arbol->nombre = nombres.nombres[i];
            arbol->izq = null;
            arbol->der = null
        else {
            var tArbol padre;
            var tArbol act;
            var bool fin
            &&
            fin = false;
            padre = null;
            act = arbol;
            
            while not fin do
                padre = act;
                if act->nombre < nombres.nombres[i] then
                    act = act->der
                else if act->nombre > nombres.nombres[i] then
                        act = act->izq
                    endif
                endif;
                if act == null then
                    fin = true
                else if act->nombre == nombres.nombres[i] then
                        fin = true
                    endif
                endif
            endwhile;
            if act == null then
                if padre->nombre < nombres.nombres[i] then
                    new padre->der;
                    padre->der->nombre = nombres.nombres[i];
                    padre->der->izq = null;
                    padre->der->der = null
                else new padre->izq;
                    padre->izq->nombre = nombres.nombres[i];
                    padre->izq->izq = null;
                    padre->izq->der = null
                endif
            endif
        }
        endif
    }
    &&
    arbol = null;
    i = 0;
    
    while i < nombres.cont do
        call inserta_nombre(arbol);
        i = i + 1
    endwhile
};

proc escribe_nombres (tArbol arbol) {
    if arbol != null then
        call escribe_nombres(arbol->izq);
        write arbol->nombre;
        nl;
        call escribe_nombres(arbol->der)
    endif
}
&&
call lee_nombres(nombres);
call construye_arbol();
write "Listado de nombres ordenado";
nl;
write "---------------------------";
nl;
call escribe_nombres(arbol)